if not config:
    configfile: "config/config.yaml"


def _get_all_targets(wildcards):
    # Default targets are the metadata TSV and sequences FASTA files
    all_targets = [
        "data/sequences.fasta",
        "data/extended_metadata.tsv"
    ]

    # Add additional targets based on upload config
    upload_config = config.get("upload", {})

    for target, params in upload_config.items():
        files_to_upload = params.get("files_to_upload", [])
        remote_file_names = params.get("remote_file_names", [])

        if len(files_to_upload) != len(remote_file_names):
            print(
                f"Skipping file upload for {target!r} because the number of",
                "files to upload does not match the number of remote file names."
            )
        elif len(remote_file_names) != len(set(remote_file_names)):
            print(f"Skipping file upload for {target!r} because there are duplicate remote file names.")
        elif "s3_dst" not in config:
            print(f"Skipping file upload for {target!r} because the destintion was not defined.")
        else:
            all_targets.extend(
                expand(
                    [f"data/upload/{target}/{{file_to_upload}}-to-{{remote_file_name}}.done"],
                    zip,
                    file_to_upload=files_to_upload,
                    remote_file_name=remote_file_names
                )
            )
    return all_targets

rule all:
    input: _get_all_targets

include: "rules/fetch_sequences.smk"

include: "rules/curate.smk"

if "s3_dst" in config:
    include: "rules/upload.smk"

